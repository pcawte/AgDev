; (c) Copyright 2007-2008 Zilog, Inc.
;   int strncmp(char *s1,char *s2,size_t n)
; Changed to remove bug of typecasting difference value to signed char - should be difference of unsigned chars
; Brendan Fletcher 19/03/2024

	ASSUME 	ADL=1

	section	.text
	public	_strncmp
_strncmp:
	ld	iy, 0
	add	iy, sp

	ld	bc, (iy+9)
	ld	a, (iy+11)
	or	a, c
	or	a, b
	jr	z, .done	; n==0 ? return(0)

	ld	hl, (iy+6)
	ld	de, (iy+3)

.cloop:
	ld	a, (de)
	or	a, a
	jr	z, .diffnul	; return the difference value if *s1=='\0'
	cpi
	jr	nz, .diff	; return the difference value if mismatch occurs
	inc	de
	jp	pe, .cloop	; if all the chars not over then loop

.diff:
	dec	hl
.diffnul:
	sub	a, (hl)
.done:
	sbc	hl, hl
	ld	l, a
	ret
